// Possible Alert: Count of rows >=1 in specific time period (e.g. aggregated and checked every 10 minutes)
DatabricksClusters
| where ActionName == "permanentDelete"
| extend User = parse_json(Identity).email
| extend Subscription = split(_ResourceId, "/")[2]
| extend Ressourcegroup = split(_ResourceId, "/")[4]
| extend Workspace = split(_ResourceId, "/")[-1]
| extend ClusterId = tostring(parse_json(RequestParams).cluster_id)
| join (
    DatabricksClusters
    | where ActionName == "deleteResult"
    | extend ClusterName = parse_json(RequestParams).clusterName
    | extend ClusterId = tostring(parse_json(RequestParams).clusterId)
    | project ClusterName, ClusterId)
    on ClusterId
| order by TimeGenerated desc
| project TimeGenerated, User, Subscription, Ressourcegroup, Workspace, ClusterName, ClusterId